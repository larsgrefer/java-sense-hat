buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        def freefairGradleVersion = "4.1.6"
        classpath "io.freefair.gradle:git-plugin:$freefairGradleVersion"
        classpath "io.freefair.gradle:github-plugin:$freefairGradleVersion"
        classpath "io.freefair.gradle:maven-plugin:$freefairGradleVersion"
        classpath "io.freefair.gradle:lombok-plugin:$freefairGradleVersion"

        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1"
        classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.4.0"
    }
}

apply plugin: "io.freefair.git-version"
apply plugin: "io.freefair.github.base"

allprojects {
    repositories {
        mavenCentral()
    }

    group = "de.larsgrefer.sense-hat"
}

github {
    slug = "larsgrefer/java-sense-hat"
    username = findProperty('githubUsername')
    token = findProperty('githubToken')
}

allprojects {
    plugins.withId("maven-publish") {
        project.apply plugin: 'de.marcphilipp.nexus-publish'

        nexusPublishing {
            repositories {
                sonatype()
            }
        }

        project.apply plugin: "signing"
        project.apply plugin: "io.freefair.maven-central.validate-poms"

        signing {
            required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }
        }

        publishing {
            publications.withType(MavenPublication) {
                pom {
                    description = provider { project.description }
                    developers {
                        developer {
                            id = 'larsgrefer'
                            name = 'Lars Grefer'
                            email = 'github@larsgrefer.de'
                            timezone = 'Europe/Berlin'
                        }
                    }
                }
            }
        }
    }
}

apply plugin: "io.codearte.nexus-staging"

nexusStaging {
    stagingProfileId = '9d2198adfd84d'
    username = findProperty('ossrhUser')
    password = findProperty('ossrhPass')
    delayBetweenRetriesInMillis = 5000
}
